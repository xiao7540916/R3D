#version 460
uniform int uLevel;
uniform float uSampleRadius;
uniform int uDstWidth;
uniform int uDstHeight;
uniform vec4 uFilter;//阈值控制
layout (binding = 0) uniform sampler2D srcTex;
layout (binding = 1, rgba32f) uniform image2D dstImage;

layout(local_size_x = 16, local_size_y = 16) in;
/*in uvec3 gl_NumWorkGroups;
in uvec3 gl_WorkGroupID;
in uvec3 gl_LocalInvocationID;
in uvec3 gl_GlobalInvocationID;
in uint gl_LocalInvocationIndex;

gl_NumWorkGroups：此变量包含传递给调度功能的工作组数。
gl_WorkGroupID：这是此着色器调用的当前工作组。每个XYZ组件将处于半开范围[0，gl_NumWorkGroups.XYZ）
gl_LocalInvocationID：这是着色器的当前调用中的工作组。每个XYZ组件将处于半开范围[0，gl_WorkGroupSize.XYZ）
gl_GlobalInvocationID：此值在此计算调度调用的所有调用中唯一标识该计算着色器的特定调用
它跟前者有着等式关系：gl_GlobalInvocationID = gl_WorkGroupID * gl_WorkGroupSize + gl_LocalInvocationID;
gl_LocalInvocationIndex：这是gl_LocalInvocationID的一维版本。它在工作组中标识此调用的索引
它跟前者有着等式关系： gl_LocalInvocationIndex =
gl_LocalInvocationID.z * gl_WorkGroupSize.x * gl_WorkGroupSize.y +
gl_LocalInvocationID.y * gl_WorkGroupSize.x +
gl_LocalInvocationID.x;*/
/*
//gl_GlobalInvocationID.xy 全局工作组的位置gl_WorkGroupID*gl_WorkGroupSize+gl_LocalInvocationID
//gl_LocalInvocationID.xy 本地工作组的序号(0,0,0)到(3,3,1);
//gl_WorkGroupID 当前工作组在全局工作组中的位置（0,0,0）到（299,224,0）
//gl_WorkGroupSize.xyz 工作组大小(4,4,1)
//gl_NumWorkGroups 工作组个数向量vec3(300,225,1);
*/
vec3 Sample(vec2 uv){
    return texture(srcTex, uv).rgb;
}
//根据filter预过滤不参加bloom的颜色
vec3 PreFilter(vec3 c){
    float brightness = max(c.r, max(c.g, c.b));
    float soft = brightness-uFilter.y;
    soft = clamp(soft, 0, uFilter.z);
    soft = soft*soft*uFilter.w;
    float contribution = max(soft, brightness-uFilter.x);
    contribution/=max(brightness, 0.00001);
    return min(c*contribution, vec3(1000000));
}
//未预先计算参数的过滤
/*vec3 PreFilter(vec3 c){
    float brightness = max(c.r, max(c.g, c.b));
    float threshold = 1.0;
    float softThreshold = 0.5;
    float knee = threshold*softThreshold;
    float soft = brightness-threshold+knee;
    soft = clamp(soft, 0, 2*knee);
    soft = soft*soft/(4*knee+0.00001);
    float contribution = max(soft, brightness-threshold);
    contribution/=max(brightness, 0.00001);
    return c*contribution;
}*/

vec3 SampleBoxFilter(vec2 uv, vec2 texelSize){
    vec4 o = texelSize.xyxy*vec2(-1, 1).xxyy*uSampleRadius;
    vec3 colxy = PreFilter(Sample(uv+o.xy));
    vec3 colzy = PreFilter(Sample(uv+o.zy));
    vec3 colxw = PreFilter(Sample(uv+o.xw));
    vec3 colzw = PreFilter(Sample(uv+o.zw));
    vec3 s = colxy+colzy+colxw+colzw;
    return s*0.25f;
}
vec3 SampleBox(vec2 uv, vec2 texelSize){
    vec4 o = texelSize.xyxy*vec2(-1, 1).xxyy*uSampleRadius;
    vec3 colxy = Sample(uv+o.xy);
    vec3 colzy = Sample(uv+o.zy);
    vec3 colxw = Sample(uv+o.xw);
    vec3 colzw = Sample(uv+o.zw);
    vec3 s = colxy+colzy+colxw+colzw;
    return s*0.25f;
}
void main()
{
    vec2 texUV = (vec2(gl_GlobalInvocationID.xy)+vec2(0.5))/vec2(uDstWidth, uDstHeight);
    vec2 texelSize = 1.0 / textureSize(srcTex, 0);
    vec3 col = vec3(0);
    if (uLevel==0){
        col = SampleBoxFilter(texUV, texelSize);
    } else {
        col = SampleBox(texUV, texelSize);
    }
    imageStore(dstImage, ivec2(gl_GlobalInvocationID.xy), vec4(col, 1.0f));
}
